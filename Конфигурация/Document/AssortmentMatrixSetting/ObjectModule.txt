
Procedure OnWrite(Cancel)
	
	Var ChangePeriod;
	
	ThisObject.AdditionalProperties.Property("ChangePeriod", ChangePeriod);
	
	If Not ChangePeriod = Undefined Then
		
		If ChangePeriod Then
			
			// Изменяем дату конца действия предыдущей действующей для данной 
			// ассортиментной матрицы настройки ассортиментной матрицы
			
			Query = New Query(
			"SELECT TOP 1
			|	AssortmentMatrixSetting.Ref,
			|	CASE
			|		WHEN AssortmentMatrixSetting.EndDate = DATETIME(1, 1, 1, 0, 0, 0)
			|				OR AssortmentMatrixSetting.EndDate >= &CurrentDate
			|			THEN DATEADD(&BeginDate, DAY, -1)
			|		ELSE UNDEFINED
			|	END AS EndDate
			|FROM
			|	Document.AssortmentMatrixSetting AS AssortmentMatrixSetting
			|WHERE
			|	AssortmentMatrixSetting.AssortmentMatrix = &AssortmentMatrix
			|	AND AssortmentMatrixSetting.Ref <> &ThisDocument
			|
			|ORDER BY
			|	AssortmentMatrixSetting.BeginDate DESC");
			
			Query.SetParameter("ThisDocument", ThisObject.Ref);
			Query.SetParameter("CurrentDate", CurrentDate());
			Query.SetParameter("BeginDate", ThisObject.BeginDate);
			Query.SetParameter("AssortmentMatrix", ThisObject.AssortmentMatrix);
			
			QuerySelection = Query.Execute().Select();
			
			If QuerySelection.Next() Then
				
				If Not QuerySelection.EndDate = Undefined Then
					
					DocumentObject = QuerySelection.Ref.GetObject();
					DocumentObject.EndDate = QuerySelection.EndDate;
					DocumentObject.AdditionalProperties.Insert("ChangePeriod", False);
					DocumentObject.Write();
					
				EndIf;
				
			EndIf;
			
		EndIf;
		
	EndIf;
	
EndProcedure
