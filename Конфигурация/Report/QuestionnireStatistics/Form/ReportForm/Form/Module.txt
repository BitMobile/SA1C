
#Region CommonProceduresAndFunctions

&AtServer
Procedure ResultDetailProcessingServer(Details, StandardProcessing)
	
	Password 	= Constants.bitmobile_Пароль.Get();
	Server 		= StrReplace(Constants.bitmobile_Сервер.Get(), "localhost", "127.0.0.1");
	Path 		= Constants.bitmobile_ПутьНаСервере.Get();
	Path 		= StrReplace(Path, "admin", "webdav");
	Port 		= Constants.bitmobile_Порт.Get();
	
	Try
	
		Connection = New HTTPConnection(Server, ?(Port = 0, 80, Port), "admin", Password);
		
	Except
		
		Connection = Undefined;
		
	EndTry;
	
	Fields = GetFields(Details);
	
	For Each Field In Fields Do
		
		If Field.Field = "Answer" Then
			
			StandardProcessing = False;
			
			Value = Field.Value;
			
			Try
				
				SnapshotUUID = New UUID(Value);
				
				SetPicture(SnapshotUUID, Connection, Path);
				
			Except
				
				ThisForm.PictureAddress = Undefined;
				ThisForm.PictureExtension = Undefined;
				ThisForm.PictureFilename = Undefined;
				
			EndTry;
			
		Else
			
			ThisForm.PictureAddress = Undefined;
			ThisForm.PictureExtension = Undefined;
			ThisForm.PictureFilename = Undefined;
			
		EndIf;
		
	EndDo;
	
EndProcedure

&AtServer
Procedure SetPicture(ID, Connection, Path)
	
	Filter = New Structure;
	Filter.Insert("FileName", ID);
	
	Sel = InformationRegisters.StorageAdditionalInformation.Select(Filter);
	
	If Sel.Next() Then
		
		BinaryDataOfFile = Sel.Storage.Get();
		
		If ValueIsFilled(BinaryDataOfFile) Then
		
			ThisForm.PictureAddress = PutToTempStorage(BinaryDataOfFile);
			ThisForm.PictureExtension = Sel.Extension;
			ThisForm.PictureFilename = Sel.FileName;
			
		Else 
			
			If Not Connection = Undefined Then 
			
				Try
					
					WebDAVFile = GetTempFileName(Sel.Extension);
					
					Connection.Get(Path + Sel.FullFileName, WebDAVFile);
					
					BinaryDataOfFile = New BinaryData(WebDAVFile);
					
					ThisForm.PictureAddress = PutToTempStorage(BinaryDataOfFile);
					ThisForm.PictureExtension = Sel.Extension;
					ThisForm.PictureFilename = Sel.FileName;
					
				Except
					
					ThisForm.PictureAddress = Undefined;
					ThisForm.PictureExtension = Undefined;
					ThisForm.PictureFilename = Undefined;
					
				EndTry;
				
			Else 
				
				ThisForm.PictureAddress = Undefined;
				ThisForm.PictureExtension = Undefined;
				ThisForm.PictureFilename = Undefined;
				
			EndIf;
						
		EndIf;	
		
	Else
		
		ThisForm.PictureAddress = Undefined;
		ThisForm.PictureExtension = Undefined;
		ThisForm.PictureFilename = Undefined;
		
	EndIf;
	
EndProcedure

&AtServer
Function GetFields(Details)
	
	Return GetFromTempStorage(DetailsData).Items.Get(Details).GetFields();
	
EndFunction

&AtServer
Function GetFilter(DataCompositionField)
	
	ReportObject = FormDataToValue(Report, Type("ReportObject"));
	DataCompositionSchema = ReportObject.DataCompositionSchema;
	DataSets = DataCompositionSchema.DataSets;
	DataSet = DataSets[0];
	Field = DataSet.Fields.Find(DataCompositionField);
	ParameterValue = Field.EditParameters.Items.Find("ChoiceParameters").Value;
	
	Filter = New Map;
	
	For Each ChoiceParameter In ParameterValue Do
		
		Filter.Insert(ChoiceParameter.Name, ChoiceParameter.Value);
		
	EndDo;
	
	Return Filter;
	
EndFunction

#EndRegion

#Region UserInterface

&AtClient
Procedure PictureAddressClick(Item, StandardProcessing)
	
	StandardProcessing = False;
	
	If ValueIsFilled(ThisForm.PictureFilename) Then
		
		GetFile(ThisForm.PictureAddress, PictureFilename + PictureExtension, True);
		
	EndIf;
	
EndProcedure

&AtClient
Procedure ResultDetailProcessing(Item, Details, StandardProcessing)
	
	ResultDetailProcessingServer(Details, StandardProcessing);
	
EndProcedure

&AtClient
Procedure SettingsComposerUserSettingsTableValueStartChoice(Item, ChoiceData, StandardProcessing)
	
	CurrentId = Item.Parent.Parent.CurrentRow;
	CurrentUserSetting = ThisForm.Report.SettingsComposer.UserSettings.GetObjectByID(CurrentId);
	CurrentUserSettingId = CurrentUserSetting.UserSettingID;
	CurrentSetting = Undefined;
	
	For Each Element In ThisForm.Report.SettingsComposer.Settings.Filter.Items Do
		
		If Element.UserSettingID = CurrentUserSettingId Then
			
			CurrentSetting = Element;
			
		EndIf;
		
	EndDo;
	
	If NOT CurrentSetting = Undefined Then
		
		If CurrentUserSetting.ComparisonType = DataCompositionComparisonType.InList OR 
			CurrentUserSetting.ComparisonType = DataCompositionComparisonType.InListByHierarchy OR
			CurrentUserSetting.ComparisonType = DataCompositionComparisonType.NotInList OR
			CurrentUserSetting.ComparisonType = DataCompositionComparisonType.NotInListByHierarchy Then
			
			Try
				
				Filter = GetFilter(CurrentSetting.LeftValue);
				ValueType = CurrentUserSetting.RightValue.ValueType;
				
				FormParameters = New Structure;
				
				If Not ValueIsFilled(ValueType) = Undefined Then
					
					StandardProcessing = False;
					
					FormParameters.Insert("FieldValueType", ValueType);
					
					Form = GetForm("CommonForm.ValueListForm", FormParameters, Item, , WindowOpenVariant.SingleWindow);
					
					For Each Row In Filter Do
							
						NewRow = Form.Filter.Add();
						NewRow.FilterName = Row.Key;
						NewRow.Value = Row.Value;
							
					EndDo;
					
					Form.ValueList.LoadValues(CurrentUserSetting.RightValue.UnloadValues());
					
					Form.Open();
					
				EndIf;
				
			Except
			EndTry;
			
		EndIf;
			
	EndIf;
	
EndProcedure

#EndRegion
