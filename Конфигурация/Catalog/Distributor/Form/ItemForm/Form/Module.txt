
#Region CommonProceduresAndFunctions

&AtServer
Procedure OnCreateAtServer(Cancel, StandardProcessing)
	
	FillContractors();
	
	FillContactList();
	
	FillRegions();
	
	FillTerritories();
	
	HandleContactsAdditionalAccessRights();
	
	SetConditionalAppearence();
	
EndProcedure

&AtServer
Procedure OnWriteAtServer(Cancel, CurrentObject, WriteParameters)
	
	If Not Cancel Then
		
		WriteContractors();
		
		WriteContactList();
		
	EndIf;
	
EndProcedure

#Region Contractors

&AtServer
Procedure FillContractors()
	
	Query = New Query(
	"SELECT ALLOWED
	|	PartnersContractorsSliceLast.Contractor,
	|	PartnersContractorsSliceLast.Default
	|FROM
	|	InformationRegister.PartnersContractors.SliceLast(, Partner = &Partner) AS PartnersContractorsSliceLast
	|WHERE
	|	PartnersContractorsSliceLast.Status <> VALUE(Enum.ValueTableRowStatuses.Deleted)");
	
	Query.SetParameter("Partner", ThisForm.Object.Ref);
	
	Result = Query.Execute().Unload();
	
	ThisForm.Contractors.Load(Result);
	
EndProcedure

&AtServer
Procedure WriteContractors()
	
	Query = New Query(
	"SELECT ALLOWED
	|	FormContractors.Contractor,
	|	FormContractors.Default
	|INTO FormContractors
	|FROM
	|	&FormContractors AS FormContractors
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|SELECT ALLOWEd
	|	PartnersContractorsSliceLast.Contractor,
	|	PartnersContractorsSliceLast.Default
	|INTO RegisterContractors
	|FROM
	|	InformationRegister.PartnersContractors.SliceLast(, Partner = &Partner) AS PartnersContractorsSliceLast
	|WHERE
	|	PartnersContractorsSliceLast.Status <> VALUE(Enum.ValueTableRowStatuses.Deleted)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|SELECT ALLOWED
	|	FormContractors.Contractor AS FormContractor,
	|	FormContractors.Default AS FormDefault,
	|	RegisterContractors.Contractor AS RegisterContractor,
	|	RegisterContractors.Default AS RegisterDefault
	|INTO Difference
	|FROM
	|	FormContractors AS FormContractors
	|		FULL JOIN RegisterContractors AS RegisterContractors
	|		ON FormContractors.Contractor = RegisterContractors.Contractor
	|WHERE
	|	(FormContractors.Contractor IS NULL 
	|				AND FormContractors.Default IS NULL 
	|			OR RegisterContractors.Contractor IS NULL 
	|				AND RegisterContractors.Default IS NULL 
	|			OR NOT FormContractors.Default = RegisterContractors.Default)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|SELECT ALLOWED
	|	&CurrentDate AS Period,
	|	&Partner AS Partner,
	|	ISNULL(Difference.FormContractor, Difference.RegisterContractor) AS Contractor,
	|	ISNULL(Difference.FormDefault, Difference.RegisterDefault) AS Default,
	|	CASE
	|		WHEN Difference.FormContractor IS NULL 
	|				AND Difference.FormDefault IS NULL 
	|			THEN VALUE(Enum.ValueTableRowStatuses.Deleted)
	|		WHEN Difference.RegisterContractor IS NULL 
	|				AND Difference.RegisterDefault IS NULL 
	|			THEN VALUE(Enum.ValueTableRowStatuses.Added)
	|		WHEN NOT Difference.FormDefault = Difference.RegisterDefault
	|			THEN VALUE(Enum.ValueTableRowStatuses.Modified)
	|	END AS Status
	|FROM
	|	Difference AS Difference");
	
	Query.SetParameter("FormContractors", ThisForm.Contractors.Unload());
	Query.SetParameter("Partner", ThisForm.Object.Ref);
	Query.SetParameter("CurrentDate", CurrentDate());
	
	Result = Query.Execute().Unload();
	
	For Each Row In Result Do
		
		RecordManager = InformationRegisters.PartnersContractors.CreateRecordManager();
		FillPropertyValues(RecordManager, Row);
		RecordManager.Write();
		
	EndDo;
	
EndProcedure

&AtServer
Function GetFilters()
	
	FiltersArray = New Array;
	
	FiltersArray.Add(GetFilter("Ref.Regions.Region", GetRegions()));
	FiltersArray.Add(GetFilter("Ref.Territories.Territory", GetTerritories()));
	FiltersArray.Add(GetFilter("Ref", GetAvailableContractors()));
	
	Return FiltersArray;
	
EndFunction

&AtServer
Function GetFilter(FieldName, List)
	
	Filter = New DataCompositionFilter;
	FilterItem = Filter.Items.Add(Type("DataCompositionFilterItem"));
	FilterItem.LeftValue = New DataCompositionField(FieldName);
	FilterItem.Use 				= True;
	FilterItem.ViewMode			= DataCompositionSettingsItemViewMode.Inaccessible;
	FilterItem.ComparisonType	= DataCompositionComparisonType.InList;
	FilterItem.RightValue		= List;
	
	Return FilterItem;
	
EndFunction

&AtServer
Function GetAvailableContractors()
	
	Query = New Query(
	"SELECT ALLOWED
	|	FormContractors.Contractor
	|INTO FormContractors
	|FROM
	|	&FormContractors AS FormContractors
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|SELECT ALLOWED
	|	PartnersContractorsSliceLast.Contractor
	|INTO PartnersContractors
	|FROM
	|	InformationRegister.PartnersContractors.SliceLast AS PartnersContractorsSliceLast
	|WHERE
	|	NOT PartnersContractorsSliceLast.Status = VALUE(Enum.ValueTableRowStatuses.Deleted)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|SELECT ALLOWED
	|	OutletsContractorsSliceLast.Contractor
	|INTO OutletsContractors
	|FROM
	|	InformationRegister.OutletsContractors.SliceLast AS OutletsContractorsSliceLast
	|WHERE
	|	NOT OutletsContractorsSliceLast.Status = VALUE(Enum.ValueTableRowStatuses.Deleted)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|SELECT ALLOWED
	|	Contractors.Ref AS Contractor
	|FROM
	|	Catalog.Contractors AS Contractors
	|		LEFT JOIN OutletsContractors AS OutletsContractors
	|		ON Contractors.Ref = OutletsContractors.Contractor
	|		LEFT JOIN PartnersContractors AS PartnersContractors
	|		ON Contractors.Ref = PartnersContractors.Contractor
	|		LEFT JOIN FormContractors AS FormContractors
	|		ON Contractors.Ref = FormContractors.Contractor
	|WHERE
	|	OutletsContractors.Contractor IS NULL 
	|	AND PartnersContractors.Contractor IS NULL 
	|	AND FormContractors.Contractor IS NULL ");
	
	Query.SetParameter("FormContractors", ThisForm.Contractors.Unload(, "Contractor"));
	Result = Query.Execute().Unload();
	
	AvailableContractors = New Array;
	
	For Each Row In Result Do
		
		AvailableContractors.Add(Row.Contractor);
		
	EndDo;
	
	Return AvailableContractors;
	
EndFunction

#EndRegion

#Region ContactList

&AtServer
Procedure FillContactList()
	
	Query = New Query(
	"SELECT ALLOWED
	|	ObjectsContacts.ContactPerson AS Contact
	|FROM
	|	InformationRegister.ObjectsContacts AS ObjectsContacts
	|WHERE
	|	ObjectsContacts.Object = &Object
	|	AND ObjectsContacts.NotActual = FALSE");
	
	Query.SetParameter("Object", Object.Ref);
	
	Result = Query.Execute().Unload();
	
	ThisForm.ContactList.Load(Result);
	
EndProcedure

&AtServer
Procedure WriteContactList()
	
	Query = New Query(
	"SELECT ALLOWED
	|	FormContacts.Contact
	|INTO FormContacts
	|FROM
	|	&FormContacts AS FormContacts
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|SELECT ALLOWED
	|	ObjectsContacts.ContactPerson
	|INTO RegisterContacts
	|FROM
	|	InformationRegister.ObjectsContacts AS ObjectsContacts
	|WHERE
	|	ObjectsContacts.Object = &Object
	|	AND ObjectsContacts.NotActual = FALSE
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|SELECT ALLOWED
	|	FormContacts.Contact AS FormContact,
	|	RegisterContacts.ContactPerson AS RegisterContact
	|INTO Difference
	|FROM
	|	FormContacts AS FormContacts
	|		FULL JOIN RegisterContacts AS RegisterContacts
	|		ON FormContacts.Contact = RegisterContacts.ContactPerson
	|WHERE
	|	(FormContacts.Contact IS NULL 
	|			OR RegisterContacts.ContactPerson IS NULL )
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|SELECT ALLOWED
	|	&Object AS Object,
	|	ISNULL(Difference.FormContact, Difference.RegisterContact) AS ContactPerson,
	|	CASE
	|		WHEN Difference.FormContact IS NULL 
	|			THEN TRUE
	|		ELSE FALSE
	|	END AS NotActual
	|FROM
	|	Difference AS Difference");
	
	Query.SetParameter("FormContacts", ThisForm.ContactList.Unload());
	Query.SetParameter("Object", ThisForm.Object.Ref);
	
	Result = Query.Execute().Unload();
	
	For Each Row In Result Do
		
		RecordManager = InformationRegisters.ObjectsContacts.CreateRecordManager();
		FillPropertyValues(RecordManager, Row);
		RecordManager.Write();
		
	EndDo;
	
EndProcedure

&AtServer
Procedure HandleContactsAdditionalAccessRights()
	
	CurrentUser = SessionParameters.CurrentUser;
	EditContactsAccessRight = Catalogs.AdditionalAccessRights.EditPartnerContacts;
	IsAdmin = Not ValueIsFilled(CurrentUser.RoleOfUser);
	HasRightToEditContacts = Not CurrentUser.RoleOfUser.AdditionalAccessRights.Find(EditContactsAccessRight) = Undefined;
	EnableContactEdit = IsAdmin OR HasRightToEditContacts;
	Items.ContactListAddContact.Enabled = EnableContactEdit;
	Items.ContactListDeleteContact.Enabled = EnableContactEdit;
	
EndProcedure

#EndRegion

#Region Regions

&AtServer
Procedure FillRegions()
	
	Query = New Query(
	"SELECT ALLOWED
	|	DistributorRegions.Region
	|INTO SavedRegions
	|FROM
	|	Catalog.Distributor.Regions AS DistributorRegions
	|WHERE
	|	DistributorRegions.Ref = &Ref
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|SELECT ALLOWED
	|	RegionCatalog.Ref AS Region,
	|	CASE
	|		WHEN SavedRegions.Region IS NULL 
	|			THEN FALSE
	|		ELSE TRUE
	|	END AS USE
	|FROM
	|	Catalog.Region AS RegionCatalog
	|		LEFT JOIN SavedRegions AS SavedRegions
	|		ON RegionCatalog.Ref = SavedRegions.Region
	|
	|ORDER BY
	|	Region HIERARCHY
	|AUTOORDER");
	Query.SetParameter("Ref", ThisForm.Object.Ref);
	Result = Query.Execute().Unload(QueryResultIteration.ByGroupsWithHierarchy);
	
	ValueToFormAttribute(Result, "Regions");
	
EndProcedure

#EndRegion

#Region Territories

&AtServer
Procedure FillTerritories()
	
	Query = New Query(
	"SELECT ALLOWED
	|	DistributorTerritories.Territory
	|INTO SavedTerritories
	|FROM
	|	Catalog.Distributor.Territories AS DistributorTerritories
	|WHERE
	|	DistributorTerritories.Ref = &Ref
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|SELECT ALLOWED
	|	TerritoryCatalog.Owner AS Region,
	|	TerritoryCatalog.Ref AS Territory,
	|	CASE
	|		WHEN SavedTerritories.Territory IS NULL 
	|			THEN FALSE
	|		ELSE TRUE
	|	END AS Use
	|FROM
	|	Catalog.Territory AS TerritoryCatalog
	|		LEFT JOIN SavedTerritories AS SavedTerritories
	|		ON TerritoryCatalog.Ref = SavedTerritories.Territory
	|
	|ORDER BY
	|	Region
	|AUTOORDER");
	
	Query.SetParameter("Ref", ThisForm.Object.Ref);
	
	Result = Query.Execute().Unload();
	
	ThisForm.Territories.Load(Result);
	
EndProcedure

#EndRegion

&AtServer
Procedure SetConditionalAppearence()
	
	// Green color for territories with current selected region
	CA = ThisForm.ConditionalAppearance;
	CAItem = CA.Items.Add();
	
	Field = CAItem.Fields.Items.Add();
	Field.Field = New DataCompositionField("TerritoriesRegion");
	Field.Use = True;
	
	Field = CAItem.Fields.Items.Add();
	Field.Field = New DataCompositionField("TerritoriesTerritory");
	Field.Use = True;
	
	Field = CAItem.Fields.Items.Add();
	Field.Field = New DataCompositionField("TerritoriesUse");
	Field.Use = True;
	
	RegionsFilterItem = CAItem.Filter.Items.Add(Type("DataCompositionFilterItem"));
	RegionsFilterItem.LeftValue = New DataCompositionField("Territories.Region");
	RegionsFilterItem.ComparisonType = DataCompositionComparisonType.Equal;
	RegionsFilterItem.RightValue = New DataCompositionField("CurrentRegion");
	
	CAItem.Appearance.SetParameterValue("BackColor", WebColors.PaleGreen);
	
	// Block use of territories that are not in current selected region
	CAItem = CA.Items.Add();
	
	Field = CAItem.Fields.Items.Add();
	Field.Field = New DataCompositionField("TerritoriesUse");
	Field.Use = True;
	
	RegionsFilterItem = CAItem.Filter.Items.Add(Type("DataCompositionFilterItem"));
	RegionsFilterItem.LeftValue = New DataCompositionField("Territories.Region");
	RegionsFilterItem.ComparisonType = DataCompositionComparisonType.NotEqual;
	RegionsFilterItem.RightValue = New DataCompositionField("CurrentRegion");
	
	CAItem.Appearance.SetParameterValue("ReadOnly", True);
	
EndProcedure

#EndRegion

#Region UserInterface

&AtClient
Procedure NotificationProcessing(EventName, Parameter, Source)
	
	If EventName = "Update" Then
		
		ThisForm.Items.ContactList.Refresh();
		
	EndIf;
	
EndProcedure

#Region Contractors

&AtClient
Procedure ContractorsDefaultOnChange(Item)
	
	CurrentData = Items.Contractors.CurrentData;
	IsNotDefaultAfterChange = CurrentData.Default;
	ThisRowIndex = ThisForm.Contractors.IndexOf(CurrentData);
	ThisRow = ThisForm.Contractors.Get(ThisRowIndex);
	
	If IsNotDefaultAfterChange Then
		
		For Each Row In ThisForm.Contractors Do
			
			Row.Default = ThisForm.Contractors.IndexOf(Row) = ThisRowIndex;
			
		EndDo;
		
	Else
		
		ThisRow.Default = True;
		
	EndIf;
	
EndProcedure

&AtClient
Procedure ContractorsChoiceProcessing(Item, SelectedValue, StandardProcessing)
	
	FilterParameters = New Structure("Contractor", SelectedValue);
	ContractorExists = ThisForm.Contractors.FindRows(FilterParameters).Count();
	
	If NOT ContractorExists Then
		
		FirstItem = ThisForm.Contractors.Count() = 0;
		NewContractorRow = ThisForm.Contractors.Add();
		NewContractorRow.Contractor = SelectedValue;
		NewContractorRow.Default = FirstItem;
		Modified = True;
		
	EndIf;
	
EndProcedure

#EndRegion

#Region ContactList

&AtClient
Procedure ContactListSelection(Item, SelectedRow, Field, StandardProcessing)
	
	CurrentData = Items.ContactList.CurrentData;
	
	If NOT CurrentData = Undefined Then
		
		OpenForm("Catalog.ContactPersons.ObjectForm", New Structure("Key", CurrentData.Contact), ThisForm);
		
	EndIf;
	
EndProcedure

&AtClient
Procedure ContactListChoiceProcessing(Item, SelectedValue, StandardProcessing)
	
	FilterParameters = New Structure("Contact", SelectedValue);
	ContactExists = ThisForm.ContactList.FindRows(FilterParameters).Count();
	
	If Not ContactExists Then
		
		NewContactRow = ThisForm.ContactList.Add();
		NewContactRow.Contact = SelectedValue;
		Modified = True;
		
	EndIf;
	
EndProcedure

#EndRegion

#Region Regions

&AtClient
Procedure RegionsBeforeCollapse(Item, Row, Cancel)
	
	Cancel = True;
	
EndProcedure

&AtClient
Procedure RegionsOnActivateRow(Item)
	
	CurrentData = ThisForm.Items.Regions.CurrentData;
	
	CurrentRegion = ?(CurrentData = Undefined, Undefined, CurrentData.Region);
	
	Filter = New FixedStructure("Region", CurrentRegion);
	ThisForm.Items.Territories.RowFilter = ?(ThisForm.Items.ShowAllTerritories.Check, Undefined, Filter);
	
	ThisForm.CurrentRegion = CurrentRegion;
	
	ThisForm.Items.Territories.Refresh();
	
EndProcedure

&AtClient
Procedure RegionsUseOnChange(Item)
	
	CurrentData = ThisForm.Items.Regions.CurrentData;
	
	If Not CurrentData = Undefined Then
		
		If Not CurrentData.Use Then
		
			Filter = New Structure;
			Filter.Insert("Region", CurrentData.Region);
			
			TerritoriesRows = ThisForm.Territories.FindRows(Filter);
			
			For Each Row In TerritoriesRows Do
				
				Row.Use = False;
				
			EndDo;
			
			Filter = New Structure("Region", CurrentData.Region);
			RowsToDelete = ThisForm.Object.Regions.FindRows(Filter);
			For Each Row In RowsToDelete Do
				
				ThisForm.Object.Regions.Delete(Row);
				
			EndDo;
			
		Else
			
			NewRow = ThisForm.Object.Regions.Add();
			NewRow.Region = CurrentData.Region;
			
		EndIf;
		
	EndIf;
	
	ThisForm.Items.Territories.Refresh();
	
EndProcedure

#EndRegion

#Region Territories

&AtClient
Procedure CheckTerritories(Value)
	
	CurrentData = ThisForm.Items.Regions.CurrentData;
	
	If Not CurrentData = Undefined Then
		
		If CurrentData.Use Then
		
			Filter = New Structure;
			Filter.Insert("Region", CurrentData.Region);
			
			Rows = ThisForm.Territories.FindRows(Filter);
			
			For Each Row In Rows Do
				
				Row.Use = Value;
				Modified = True;
				
				Filter = New Structure("Territory", Row.Territory);
				
				If Value = True Then
				
					If ThisForm.Object.Territories.FindRows(Filter).Count() = 0 Then
						
						NewRow = ThisForm.Object.Territories.Add();
						NewRow.Territory = Row.Territory;
						
					EndIf;
					
				Else
					
					RowsToDelete = ThisForm.Object.Territories.FindRows(Filter);
					
					For Each RowToDelete In RowsToDelete Do
						
						ThisForm.Object.Territories.Delete(RowToDelete);
						
					EndDo;
					
				EndIf;
				
			EndDo;
			
		EndIf;
		
	EndIf;
	
EndProcedure

&AtClient
Procedure TerritoriesUseOnChange(Item)
	
	CurrentData = ThisForm.Items.Territories.CurrentData;
	
	If Not CurrentData = Undefined Then
		
		If CurrentData.Use Then
			
			NewRow = ThisForm.Object.Territories.Add();
			NewRow.Territory = CurrentData.Territory;
			
		Else
			
			Filter = New Structure("Territory", CurrentData.Territory);
			RowsToDelete = ThisForm.Object.Territories.FindRows(Filter);
			
			For Each Row In RowsToDelete Do
				
				ThisForm.Object.Territories.Delete(Row);
				
			EndDo;
			
		EndIf;
		
	EndIf;
	
EndProcedure

#EndRegion

#Region Commands

&AtClient
Procedure AddContractor(Command)
	
	ChoiceForm = GetForm("Catalog.Contractors.ChoiceForm", , Items.Contractors);
	Filter = ChoiceForm.List.Filter;
	
	FilterItems = GetFilters();
	
	For Each FilterItem In FilterItems Do
		
		NewRow = Filter.Items.Add(Type("DataCompositionFilterItem"));
		FillPropertyValues(NewRow, FilterItem);
		
	EndDo;
	
	ChoiceForm.CloseOnChoice = False;
	
	OpenForm(ChoiceForm);
	
EndProcedure

&AtServer
Function GetTerritories()
	
	TerritoriesVT = ThisForm.Object.Territories.Unload(, "Territory");
	TerritoriesArray = New Array;
	
	For Each Row In TerritoriesVT Do
		
		TerritoriesArray.Add(Row.Territory);
		
	EndDo;
	
	Return TerritoriesArray;
	
EndFunction

&AtServer
Function GetRegions()
	
	RegionsVT = ThisForm.Object.Regions.Unload(, "Region");
	RegionsArray = New Array;
	
	For Each Row In RegionsVT Do
		
		RegionsArray.Add(Row.Region);
		
	EndDo;
	
	Return RegionsArray;
	
EndFunction

&AtClient
Procedure RemoveContractor(Command)
	
	CurrentData = Items.Contractors.CurrentData;
	
	If Not CurrentData = Undefined Then
		
		CurrentDataIndex = ThisForm.Contractors.IndexOf(CurrentData);
		ThisForm.Contractors.Delete(CurrentDataIndex);
		Modified = True;
		
	EndIf;
	
	If ThisForm.Contractors.Count() Then
		
		FilterParameters = New Structure("Default", True);
		NoDefault = NOT ThisForm.Contractors.FindRows(FilterParameters).Count();
		
		If NoDefault Then
			
			ThisForm.Contractors[0].Default = True;
			
		EndIf;
		
	EndIf;
	
EndProcedure

&AtClient
Procedure AddContact(Command)
	
	ChoiceForm = GetForm("Catalog.ContactPersons.ChoiceForm", , Items.ContactList);
	
	ChoiceForm.CloseOnChoice = False;
	
	OpenForm(ChoiceForm);
	
EndProcedure

&AtClient
Procedure DeleteContact(Command)
	
	CurrentData = Items.ContactList.CurrentData;
	
	If Not CurrentData = Undefined Then
		
		CurrentDataIndex = ThisForm.ContactList.IndexOf(CurrentData);
		ThisForm.ContactList.Delete(CurrentDataIndex);
		Modified = True;
		
	EndIf;
	
EndProcedure

&AtClient
Procedure CheckAllTerritories(Command)
	
	CheckTerritories(True);
	
EndProcedure

&AtClient
Procedure UncheckAllTerritories(Command)
	
	CheckTerritories(False);
	
EndProcedure

&AtClient
Procedure ShowAllTerritories(Command)
	
	ThisForm.Items.ShowAllTerritories.Check = Not ThisForm.Items.ShowAllTerritories.Check;
	ShowAll = ThisForm.Items.ShowAllTerritories.Check;
	CurrentData = ThisForm.Items.Regions.CurrentData;
	RegionSelected = Not CurrentData = Undefined;
	ThisForm.Items.Territories.RowFilter = ?(ShowAll AND RegionSelected, Undefined, New FixedStructure("Region", ThisForm.Items.Regions.CurrentData.Region));
	
	CurrentData = ThisForm.Items.Regions.CurrentData;
	
	If Not CurrentData = Undefined Then
		
		CurrentRegion = CurrentData.Region;
		
	EndIf;
	
EndProcedure

#EndRegion

#EndRegion